#version 450 core

in vec3 fragColor;

in vec3 fragPos;
in vec3 fragNormal;
in vec2 fragTexCoord;

out vec4 color;

uniform sampler2D tex;
uniform float ambientStr	= 0.05f;
uniform vec3 ambientColor	= vec3(1.0f, 1.0f, 1.0f);
uniform vec3 lightColor		= vec3(1.0f, 1.0f, 1.0f);
uniform vec3 lightPos		= vec3(-2.0f, 6.0f, 3.0f);

uniform vec3 camPos;
uniform float lightSpecStr	= 1.0f;
uniform float shininess		= 256.0f;

// for RIM lighting
uniform int rimExponent	= 4;
uniform vec3 rimColor 	= vec3 (1.0f, 0.0f, 0.0f);

void main()
{
	// Ambient
	vec3 ambient = ambientStr * ambientColor;

	// Light Direction
	vec3 norm = normalize(fragNormal);
	vec3 lightDir = normalize(fragPos - lightPos);

	// Diffuse Coloring
	float diffuseStr = max(dot(norm, -lightDir), 0.0f);
	vec3 diffuse = diffuseStr * lightColor;	

	// Specular Highlight
	vec3 negViewDir = normalize(camPos - fragPos);
	vec3 halfwayVec = normalize(-lightDir + negViewDir);
	float spec = pow(max(dot(norm, halfwayVec),0.0f), shininess);
	vec3 specular = lightSpecStr * spec * lightColor;

	// Rim Lighting
	float rimFactor = 1.0 - dot(norm, negViewDir); // Claculate the rim factor
	rimFactor = smoothstep(0.0, 1.0, rimFactor);   // Constrain it to the range 0-1
	rimFactor = pow(rimFactor, rimExponent);	// Raise it to the rim exponent
	vec3 rim = rimFactor * rimColor * lightColor;	// Calculate the final rim component

	color = vec4(ambient + diffuse + specular + rim, 1.0f) * (texture(tex, fragTexCoord));
}